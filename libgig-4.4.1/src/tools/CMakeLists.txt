
macro(add_tool NAME)
    add_executable(${NAME} ${NAME}.cpp)
    target_link_libraries(${NAME} PRIVATE libgig)
	if(uuid_FOUND)
    	target_link_libraries(${NAME} PRIVATE uuid)
	endif()
	if(sndfile_FOUND)
		target_compile_definitions(${NAME} PRIVATE HAVE_SNDFILE=1)
		target_link_libraries(${NAME} PRIVATE sndfile)
	endif()

endmacro(add_tool)

add_executable(akaidump akaidump.cpp)
target_link_libraries(akaidump PRIVATE libakai)

if(NOT sndfile_FOUND)
	message(WARNING "sndfile library not found: akaiextract, gigextract and sf2extract will not be built")
endif()
#These tools are not yet ported to msvc
if(NOT MSVC)
	if(sndfile_FOUND)
		add_executable(akaiextract akaiextract.cpp)
		target_compile_definitions(akaiextract PRIVATE HAVE_SNDFILE=1)
		target_include_directories(akaiextract PRIVATE ${sndfile_INCLUDEDIR})
		target_link_libraries(akaiextract PRIVATE libakai sndfile)
	endif()
	add_tool(gig2mono)
	add_tool(gig2stereo)
else()
	message(WARNING "akaiextract, gig2mono and gig2stereo aren't yet ported to msvc")
endif()

if(sndfile_FOUND)
	add_tool(gigextract)
	target_include_directories(gigextract PRIVATE ${sndfile_INCLUDEDIR})
	target_link_libraries(gigextract PRIVATE sndfile)
	add_tool(sf2extract)
	target_include_directories(sf2extract PRIVATE ${sndfile_INCLUDEDIR})
	target_link_libraries(sf2extract PRIVATE sndfile)
	add_tool(sftrimmer)
	target_include_directories(sftrimmer PRIVATE ${sndfile_INCLUDEDIR})
	target_link_libraries(sftrimmer PRIVATE sndfile)
endif()

add_tool(dlsdump)
add_tool(gigdump)
add_tool(gigmerge)
add_tool(korg2gig)
add_tool(korgdump)
add_tool(rifftree)
add_tool(sf2dump)
